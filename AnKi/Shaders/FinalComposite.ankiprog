// Copyright (C) 2009-present, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki mutator FILM_GRAIN 0 1
#pragma anki mutator BLOOM_ENABLED 0 1
#pragma anki mutator DBG_ENABLED 0 1

#include <AnKi/Shaders/QuadVert.hlsl>

#pragma anki technique_start vert
#pragma anki technique_end vert

#pragma anki technique_start pixel
#include <AnKi/Shaders/Functions.hlsl>
#include <AnKi/Shaders/MotionBlur.hlsl>

SamplerState g_nearestAnyClampSampler : register(s0);
SamplerState g_linearAnyClampSampler : register(s1);
SamplerState g_trilinearRepeatSampler : register(s2);
Texture2D<RVec4> g_lightShadingRt : register(t0);
Texture2D<RVec4> g_ppsBloomLfRt : register(t1);
Texture3D<RVec4> g_lut : register(t2);
Texture2D g_motionVectorsRt : register(t3);
Texture2D g_depthRt : register(t4);
#if DBG_ENABLED
Texture2D<RVec4> g_dbgOutlineRt : register(t5);
#endif

struct Constants
{
	U32 m_motionBlurSamples;
	F32 m_filmGrainStrength;
	U32 m_frameCount;
	U32 m_padding;
};
ANKI_FAST_CONSTANTS(Constants, g_consts)

RVec3 colorGrading(RVec3 color)
{
	Vec3 lutSize;
	g_lut.GetDimensions(lutSize.x, lutSize.y, lutSize.y);

	const RVec3 lutScale = ((lutSize.x - 1.0) / lutSize.x).xxx;
	const RVec3 lutOffset = (1.0 / (2.0 * lutSize.x)).xxx;

	color = min(color, RVec3(1.0, 1.0, 1.0));
	const RVec3 lutCoords = color * lutScale + lutOffset;
	return g_lut.SampleLevel(g_trilinearRepeatSampler, lutCoords, 0.0).rgb;
}

RVec3 main(VertOut input) : SV_TARGET0
{
	const Vec2 uv = input.m_uv;
	RVec3 outColor;

	if(g_consts.m_motionBlurSamples > 0u)
	{
		outColor =
			motionBlur(g_motionVectorsRt, g_nearestAnyClampSampler, g_lightShadingRt, g_linearAnyClampSampler, uv, g_consts.m_motionBlurSamples);
	}
	else
	{
		outColor = g_lightShadingRt.SampleLevel(g_linearAnyClampSampler, uv, 0.0).rgb;
	}

#if BLOOM_ENABLED
	const RVec3 bloom = g_ppsBloomLfRt.SampleLevel(g_linearAnyClampSampler, uv, 0.0).rgb;
	outColor += bloom;
#endif

	outColor = colorGrading(outColor);

#if FILM_GRAIN
	const F32 dt = 1.0;
	outColor = filmGrain<F32>(outColor, uv, g_consts.m_filmGrainStrength, F32(g_consts.m_frameCount % 0xFFFFu) * dt);
#endif

#if DBG_ENABLED
	const RVec4 dbg = g_dbgOutlineRt.SampleLevel(g_linearAnyClampSampler, uv, 0.0);
	outColor = lerp(outColor, dbg.rgb, dbg.a);
#endif

	return outColor;
}

#pragma anki technique_end pixel
